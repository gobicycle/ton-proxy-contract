int equal_slices (slice a, slice b) asm "SDEQ";

() recv_internal (int balance, int msg_value, cell in_msg_full, slice in_msg_body) {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();
    slice ds = get_data().begin_parse();
    slice owner_address = ds~load_msg_addr();
    throw_unless(403, equal_slices(sender_address, owner_address));
    while (in_msg_body.slice_refs()) {
        send_raw_message(in_msg_body~load_ref(), 160);
    }
}
